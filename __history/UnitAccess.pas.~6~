unit UnitAccess;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes,
  Vcl.Graphics, Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.Grids, Vcl.DBGrids,
  Data.DB, FireDAC.Comp.Client, FireDAC.Stan.Param, FireDAC.Comp.DataSet,
  Vcl.StdCtrls, UnitGlobals, FireDAC.Stan.Intf, FireDAC.Stan.Option,
  FireDAC.Stan.Error, FireDAC.DatS, FireDAC.Phys.Intf, FireDAC.DApt.Intf,
  FireDAC.Stan.Async, FireDAC.DApt;

type
  TAccessForm = class(TForm)
    DBGridServices: TDBGrid;
    DBGridUsersRoles: TDBGrid;
    DataSourceServices: TDataSource;
    DataSourceUsersRoles: TDataSource;
    FDQueryServices: TFDQuery;
    FDQueryUsersRoles: TFDQuery;
    Label1: TLabel;
    Label2: TLabel;

    procedure FormShow(Sender: TObject);
    procedure DBGridServicesCellClick(Column: TColumn);
  private
    procedure LoadServices;
    procedure LoadUsersRoles(ServiceID: Integer);
    function IsCurrentUserAdmin: Boolean;
  public
  end;

var
    AccessForm: TAccessForm;

implementation

{$R *.dfm}

procedure TAccessForm.FormShow(Sender: TObject);
begin
  if not IsCurrentUserAdmin then
  begin
    ShowMessage('Доступ дозволено лише адміністраторам.');
    Close;
    Exit;
  end;

  LoadServices;
end;

procedure TAccessForm.LoadServices;
begin
  FDQueryServices.Connection := FDConnectionGlobal;
  FDQueryServices.SQL.Text := 'SELECT SERVICE_ID, NAME FROM SERVICES ORDER BY NAME';
  FDQueryServices.Open;
end;

procedure TAccessForm.DBGridServicesCellClick(Column: TColumn);
var
  ServiceID: Integer;
begin
  if FDQueryServices.IsEmpty then Exit;
  ServiceID := FDQueryServices.FieldByName('SERVICE_ID').AsInteger;
  LoadUsersRoles(ServiceID);
end;

procedure TAccessForm.LoadUsersRoles(ServiceID: Integer);
begin
  FDQueryUsersRoles.Close;
  FDQueryUsersRoles.Connection := FDConnectionGlobal;
  FDQueryUsersRoles.SQL.Text :=
    'SELECT u.USER_ID, u.USERNAME, r.NAME AS ROLE_NAME ' +
    'FROM USER_SERVICE_ROLES usr ' +
    'JOIN USERS u ON usr.USER_ID = u.USER_ID ' +
    'JOIN ROLES r ON usr.ROLE_ID = r.ROLE_ID ' +
    'WHERE usr.SERVICE_ID = :s_id';
  FDQueryUsersRoles.ParamByName('s_id').AsInteger := ServiceID;
  FDQueryUsersRoles.Open;
end;

function TAccessForm.IsCurrentUserAdmin: Boolean;
var
  Q: TFDQuery;
begin
  Q := TFDQuery.Create(nil);
  try
    Q.Connection := FDConnectionGlobal;
    Q.SQL.Text :=
      'SELECT COUNT(*) FROM USER_SERVICE_ROLES usr ' +
      'WHERE usr.USER_ID = :u_id AND usr.ROLE_ID = 1';
    Q.ParamByName('u_id').AsInteger := CurrentUserID;
    Q.Open;
    Result := Q.Fields[0].AsInteger > 0;
  finally
    Q.Free;
  end;
end;

end.

